#region Prolog

#****Begin: Generated Statements***
#****End: Generated Statements****


# ===============================================================================================================
#GENERALCOMMENT             Descrição:          	Carrega dados CSV para o Cubo Mapa 
#GENERALCOMMENT             Data Criação:    	01/2026
#
#LASTCHANGE                 Alterações:
#LASTCHANGE                 Responsavel Alterações: Miranda / Helder
# ===============================================================================================================

# ===============================================================================================================
#PROLOGCOMMENT               Descrição:            Define arquivo fonte 
# ===============================================================================================================

# =================================================================================
# 									Glosário 
# =================================================================================
# Prefixos de Variáveis 
#
# v - Variáveis de entrada		- relacionado a origem de dados do turbo
# p - Parâmetros de entrada		- relacionado ao input realizado pelo usuário no momento de disparo do processo
# c - Constantes			- variáveis que receberão um valor fixo (Não serão alteradas durante o processo)
# n - Numerica
# s - String (Texto)
#
# Variáveis que deverão ser alteradas
# cCuboDestino			- Armazena o nome do cubo de destino
#
#
# Variáveis configuradas no cubo de controle 
# cCaminhoArquivoFonte 		- Busca o caminho de origem do processo no cubo de controle
# cCaminhoArquivoRejeitado 		- Busca o caminho de rejeitado do processo no cubo de controle
# cPrefixoArquivoFonte 		- Busca o prefixo do arquivo de origem do processo no cubo de controle
# cUtilizarDebug		 	- Busca no cubo de controle se ao executar o processo, será utilizado o modo Debug (Grava em arquivo texto o que seria gravado no cubo)
# cGravarLogAlteracao		- Busca no cubo de controle se ao executar o processo, serão gravados as alterações no cubo de destino
#
#
# Variáveis que não precisam ser alteradas
# cCuboParametro 			- Armazena o nome do cubo de parâmetro utilizado
# cCuboPropriedade		- Armazena o nome do cubo de sistema onde estão as principais propriedades dos cubos
# cExisteCabecalhoRejeitado		- Variável que irá controlar o cabeçalho do arquivo de rejeitado sejá criado uma única vez
# cExisteRegistroRejeitado 		- Variável que irá controlar se o registro possuí algum erro de integridade ou validação dos dados
# cHorarioInicio			- Armazena o horário de inicio do processo
# cMensagemRegistro		- Armazena a mensagem de erro/log do registro
# cMensagemProcesso		- Armazena a mensagem de erro do processo
# cProcesso			- Armazena o nome do processo
# cQtdRegistroRejeitado		- Contador de registros rejeitados
# cTipoErro			- Controla o tipo de erro (Erro 1 - Prologo / Erro 2 - Dados)
# cUsuario			- Armazena o nome do usuário ou chore que executou o processo

# =================================================================================
# Variáveis do padrão CTI que devem ser alteradas
# =================================================================================


# Informações do cubo de Destino
# ========================================
cCuboDestino		= 'MAP.030.Region';

cCuboReject                                      = 'SYS.250.Rejeitados';

# =================================================================================
# Variáveis do padrão CTI não devem ser alteradas
# =================================================================================

# Recupera o usuário que executou o processo
# ========================================
cUsuario = IF( DimIx( '}Clients' , TM1User() ) = 0, 'Executed by Chore' , ATTRS('}Clients', TM1User(), '}TM1_DefaultDisplayValue') );
  

  
# Variáveis de controle do processo
# ========================================
cCuboParametro			= 'SYS.150.Controle_Cargas';
cCuboPropriedade			= '}CubeProperties';
cHorarioInicio			= Now();
cProcesso			= GetProcessName();
cMensagemRegistro	 	= '';
cMensagemProcesso 		= '';
cExisteRegistroRejeitado 		= 0;
cExisteCabecalhoRejeitado 		= 0;
cQtdRegistroRejeitado		= 0;
cTipoErro				= 0;
cContadorRegistro 			= 0;
cQtdRegistroRejeitadoCubo                    = 0;


# Variáveis do cubo de controle
# ========================================
cCaminhoArquivoFonte 		= CellGetS( cCuboParametro , cProcesso , 'Caminho do Arquivo Fonte' );
cCaminhoArquivoRejeitado  		= CellGetS( cCuboParametro , cProcesso , 'Caminho do Arquivo de Rejeitado' );
cPrefixoArquivoFonte 		= CellGetS( cCuboParametro , cProcesso , 'Prefixo do Arquivo' );
cUtilizarDebug		 	= CellGetS( cCuboParametro , cProcesso , 'Debug (S/N)' );
cGravarLogAlteracao		= CellGetS( cCuboParametro , cProcesso , 'Gerar Log de Alteração (S/N)' );



# Tratamento para os campos vindos do cubo de controle que recebem (S/N)
# ========================================
cUtilizarDebug		 	= IF( cUtilizarDebug @= 'S' , 'S' , 'N' );
cGravarLogAlteracao		= IF( cGravarLogAlteracao @= 'S' , 'S' , 'N' );




# =================================================================================
# Define variáveis locais de arquivo
# =================================================================================
cDelimitadorArquivo 		= ';';
cSeparadorMilhar			= '.';
cSeparadorDecimal			= ',';

DataSourceASCIIDelimiter 		= cDelimitadorArquivo;
DataSourceASCIIThousandSeparator 	= cSeparadorMilhar;
DataSourceASCIIDecimalSeparator 	= cSeparadorDecimal;
DataSourceQuoteCharacter 		= '';


# =================================================================================
# Define o Log inicial da carga como falha
# Registra esse log logo no inicio para garantir que tenha algum log de falha registrado
# Caso o processo não consiga chegar no próximo registro de log por alguma falha inesperada
# =================================================================================
cMensagemProcesso	= 'Processo com falha, contate o administrador para verificar os logs de erro do sistema' ;
s01			= TimSt( cHorarioInicio , '\Y-\m-\d \hh\im\ss' );
s02 			= cUsuario ;
s03 			= cMensagemProcesso ;
s04 			= '0' ;
s05 			= TimSt( cHorarioInicio - Now, '\Y-\m-\d \hh\im\ss' );

CellPutS( s01 , cCuboParametro, cProcesso, 'DataHora Inicio' );
CellPutS( s01 , cCuboParametro, cProcesso, 'DataHora Fim' );
CellPutS( s02 , cCuboParametro, cProcesso, 'Usuário' );
CellPutS( s03 , cCuboParametro, cProcesso, 'Mensagem de Log' );
CellPutS( s04 , cCuboParametro, cProcesso, 'Registros Processados' );
CellPutS( s05 , cCuboParametro, cProcesso, 'Tempo de Execução' );





# =================================================================================
# Validação do arquivo de origem e pasta
# Teste 1 - Se o caminho do arquivo fonte foi preenchido no cubo de controle
# Teste 2 - Se o caminho do arquivo rejeitado foi preenchido no cubo de controle
# Teste 3 - Se o diretório do arquivo fonte existir
# Teste 4 - Se o diretório do arquivo de rejeitado/debug existir
# Teste 5 - Se o arquivo fonte existe na pasta informada
# =================================================================================


# Define o nome dos arquivos
# ======================================================
sPrefixo			= cPrefixoArquivoFonte;
cSufixoRejeitado		= IF( cUtilizarDebug @= 'S' , 'Debug' , 'Reject' );

cNomeArquivoFonte	=  sPrefixo  | '.csv';
cNomeArquivoRejeitado	=  sPrefixo  | '_' | cSufixoRejeitado | '.csv';



# Aborta o processo se o caminho do arquivo fonte não foi informado
# ======================================================
IF( cCaminhoArquivoFonte @= '' );
   cTipoErro = 1;
   cMensagemProcesso = 'O caminho onde esta o arquivo fonte não foi informado, informe no cubo:   "' | cCuboParametro | '"' ;
   DataSourceType = 'NULL';
   ItemReject( cMensagemProcesso );
ENDIF;



# Aborta o processo se o caminho do arquivo reject não foi informado
# ======================================================
# IF( cCaminhoArquivoRejeitado  @= '' );
#    cTipoErro = 1;
#    cMensagemProcesso = 'O caminho onde deverá ser salvo o arquivo com registros rejeitados não foi informado, informe no cubo:   "' | cCuboParametro | '"' ;
#    DataSourceType = 'NULL';
#    ItemReject( cMensagemProcesso );
# ENDIF;



# Insere o caractere "\" no final do caminho caso ainda não tenha
# ======================================================
cCaminhoArquivoFonte 	= IF( SUBST( cCaminhoArquivoFonte , LONG( cCaminhoArquivoFonte ) , 1 ) @<> '\' , cCaminhoArquivoFonte | '\' , cCaminhoArquivoFonte ); 
#cCaminhoArquivoRejeitado  	= IF( SUBST( cCaminhoArquivoRejeitado  , LONG( cCaminhoArquivoRejeitado  ) , 1 ) @<> '\' , cCaminhoArquivoRejeitado  | '\' , cCaminhoArquivoRejeitado  );



# Aborta o processo se o diretorio do arquivo fonte não existir
# ======================================================
IF( FileExists( cCaminhoArquivoFonte ) = 0 );
   cTipoErro = 1;
   cMensagemProcesso = 'O diretório especificado para o arquivo fonte  "' | cCaminhoArquivoFonte | '"   não existe, informe no cubo:   "' | cCuboParametro | '"' ;
   DataSourceType = 'NULL';
   ItemReject( cMensagemProcesso );
ENDIF;



# Aborta o processo se o diretorio dos arquivos rejeitados não existir
# ======================================================
# IF( FileExists( cCaminhoArquivoRejeitado) = 0 );
#    cTipoErro = 1;
#    cMensagemProcesso = 'O diretório especificado para o arquivo com registros rejeitados  "' | cCaminhoArquivoRejeitado | '"   não existe, informe no cubo:   "' | cCuboParametro | '"' ;
#    DataSourceType = 'NULL';
#    ItemReject( cMensagemProcesso );
# ENDIF;



# Monta o caminho completo 
# ======================================================
cNomeArquivoFonteCompleto 	= cCaminhoArquivoFonte | cNomeArquivoFonte;
#cNomeArquivoRejeitadoCompleto	= cCaminhoArquivoRejeitado | cNomeArquivoRejeitado;



# Aborta o processo se o arquivo fonte não existir na pasta de origem
# ======================================================
IF( FileExists( cNomeArquivoFonteCompleto ) = 0 );
   cTipoErro = 1;
   cMensagemProcesso = 'O arquivo   "' | cNomeArquivoFonte | '"   não existe no diretorio:   "' | cCaminhoArquivoFonte | '"' ;
   DataSourceType = 'NULL';
   ItemReject( cMensagemProcesso );
ENDIF;




# =================================================================================
#
# Definição da Origem de Dados
#
# =================================================================================

# Define o arquivo fonte como a nova fonte de dados deste processo
# ======================================================
DatasourceNameForServer		= cNomeArquivoFonteCompleto ;
DatasourceNameForClient		= cNomeArquivoFonteCompleto ;

# =================================================================================
#
# Definição View de Limpeza do Cudo de Destino
#
# =================================================================================

cSuprimirConsolidado	= 'S';
cSuprimirZero		= 'S'; 
cSuprimirCamposCalculados	= 'S';
cViewTemporaria	 	= 'S';
nViewTemporaria		= If( cViewTemporaria @= 'S', 1, 0);



# Busca parametro para ativação de Log do Cubo
# ======================================================
sLog       	= IF ( cGravarLogAlteracao @= 'N', 'NO', 'YES' );



# Grava no cubo de Log
# ======================================================
CellPutS( sLog, cCuboPropriedade, cCuboDestino, 'LOGGING' );

# =================================================================================
#
# Definição View de Limpeza do Cudo de Rejeitados
#
# =================================================================================

cSuprimirConsolidado			= 'S';
cSuprimirZero					= 'S'; 
cSuprimirCamposCalculados		= 'S';
cViewTemporariaRejeitado	 	= 'S';
nViewTemporariaRejeitado		= If( cViewTemporariaRejeitado @= 'S', 1, 0);

# Define nome da view e subset de limpeza
# ======================================================
cViewDestinoRejeitado	= 'processo.' | cProcesso | '.' | 'Limpeza' | '.' | TimSt( cHorarioInicio, '\Y-\m-\d \hh\im\ss' );
cSubsetDestinoRejeitado	= cViewDestinoRejeitado;

cViewDestino	= 'processo.' | cProcesso | '.' | 'Limpeza' | '.' | TimSt( cHorarioInicio, '\Y-\m-\d \hh\im\ss' );
cSubsetDestino	= cViewDestino;

# Cria a view que será utilizada para a limpeza
# ======================================================
ViewCreate( cCuboReject , cViewDestinoRejeitado , nViewTemporariaRejeitado );
ViewCreate( cCuboDestino ,cViewDestino , nViewTemporaria );

# Cria os subsets que serão utilizados para filtrar a view de limpeza cubo destino
# ======================================================
#Apagar data de atualização

cDimensao = 'MAP.M.030.Region';
cElemento = 'Data Atualização';
If( cDimensao @<> '');
  SubsetCreate( cDimensao, cSubsetDestino, nViewTemporaria );  
  ViewSubsetAssign(  cCuboDestino , cViewDestino, cDimensao , cSubsetDestino );  
  SubsetElementInsert( cDimensao, cSubsetDestino, cElemento	, 1 );
EndIf;

# Cria os subsets que serão utilizados para filtrar a view de limpeza
# ======================================================

cDimensaoRejeitado	= '}Processes';
cElemento	= cProcesso;
If( cDimensaoRejeitado @<> '');
  SubsetCreate( cDimensaoRejeitado, cSubsetDestinoRejeitado , nViewTemporariaRejeitado );  
  ViewSubsetAssign( cCuboReject , cViewDestinoRejeitado, cDimensaoRejeitado , cSubsetDestinoRejeitado );  
  SubsetElementInsert( cDimensaoRejeitado, cSubsetDestinoRejeitado, cElemento	, 1 );
EndIf;

cDimensaoRejeitado	= '}Clients';
cElemento	= cUsuario;
If( cDimensaoRejeitado @<> '');
  SubsetCreate( cDimensaoRejeitado, cSubsetDestinoRejeitado , nViewTemporariaRejeitado );  
  ViewSubsetAssign( cCuboReject , cViewDestinoRejeitado, cDimensaoRejeitado , cSubsetDestinoRejeitado );  
  SubsetElementInsert( cDimensaoRejeitado, cSubsetDestinoRejeitado, cElemento	, 1 );
EndIf;

# Configuração da View
# ======================================================
ViewExtractSkipCalcsSet( cCuboReject, cViewDestinoRejeitado, If( cSuprimirConsolidado @= 'S', 1, 0) );
ViewExtractSkipZeroesSet( cCuboReject, cViewDestinoRejeitado, If( cSuprimirZero @= 'S', 1, 0)  );
ViewExtractSkipRuleValuesSet( cCuboReject, cViewDestinoRejeitado, If( cSuprimirCamposCalculados @= 'S', 1, 0) );

ViewExtractSkipCalcsSet( cCuboDestino, cViewDestino, If( cSuprimirConsolidado @= 'S', 1, 0) );
ViewExtractSkipZeroesSet(  cCuboDestino, cViewDestino, If( cSuprimirZero @= 'S', 1, 0)  );
ViewExtractSkipRuleValuesSet(  cCuboDestino, cViewDestino, If( cSuprimirCamposCalculados @= 'S', 1, 0) );

# Apaga os valores da View criada
# ======================================================
ViewZeroOut( cCuboReject , cViewDestinoRejeitado );
ViewZeroOut(cCuboDestino, cViewDestino);
#endregion
#region Metadata

#****Begin: Generated Statements***
#****End: Generated Statements****


# ===============================================================================================================
#METADATACOMMENT        Descrição:  Adiciona elementos na dimensão principal do cubo mapa
# ===============================================================================================================


# =================================================================================
# Atualiza dimensão do Mapa
# =================================================================================
sElm = vCod_Elemento;
sDim = 'MAP.D.Region';

If( sElm @<> '' & DimIx( sDim, sElm ) = 0 );
    DimensionElementInsert( sDim, '', sElm,  'n' );
EndIf;
#endregion
#region Data

#****Begin: Generated Statements***
#****End: Generated Statements****


# ===============================================================================================================
#DATACOMMENT        Descrição:  Grava dados no cubo
# ===============================================================================================================

# ===================================================================================
# Gera contador para o arquivo de debug ou registros rejeitados o e quantid. total de registros processados
# ===================================================================================
cContadorRegistro = cContadorRegistro + 1;
sContadorRegistro = NumberToString( cContadorRegistro );


# ===================================================================================
# Retira os espaços em branco
# ===================================================================================
vCod_Elemento		= Trim( vCod_Elemento );
vDesc_Elemento	= Trim( vDesc_Elemento );

vH1_Nivel_1		= Trim( vH1_Nivel_1 );
vH1_Desc_Nivel_1	= Trim( vH1_Desc_Nivel_1 );
vH1_Nivel_2		= Trim( vH1_Nivel_2 );
vH1_Desc_Nivel_2	= Trim( vH1_Desc_Nivel_2 );
vH1_Nivel_3		= Trim( vH1_Nivel_3 );
vH1_Desc_Nivel_3	= Trim( vH1_Desc_Nivel_3 );

vH2_Nivel_1		= Trim( vH2_Nivel_1 );
vH2_Nivel_2	= Trim( vH2_Nivel_2 );

vCod_BPC = Trim( vCod_BPC );


# ===================================================================================
# Pula o Registro se todas as colunas estiverem vazias
# ===================================================================================
IF( '' @= vCod_Elemento	 
  & '' @= vDesc_Elemento 
  & '' @= vH1_Nivel_1 
  & '' @= vH1_Desc_Nivel_1
  & '' @= vH1_Nivel_2 
  & '' @= vH1_Desc_Nivel_2
  & '' @= vH1_Nivel_3 
  & '' @= vH1_Desc_Nivel_3
  & '' @= vH2_Nivel_1 
  & '' @= vH2_Nivel_2    
  & '' @= vCod_BPC    
  );
  ItemSkip;
EndIf;



# ===================================================================================
# Marca o registro para rejeitar 
# Caso a variável for obrigatória e estiver em branco
# ===================================================================================
sElm	= vCod_Elemento;
sCol	= 'Codigo';

If( cExisteRegistroRejeitado = 0 );
   If( sElm @= '' );
      cMensagemRegistro = 'A coluna    [ ' | sCol | ' ]    está em branco e é obrigatório ter algum valor';
      cExisteRegistroRejeitado = 1;
   EndIf;
EndIf;



# ===================================================================================
# Marca o registro para rejeitar 
# Caso a variável for obrigatória e estiver em branco
# ===================================================================================
sElm	= vDesc_Elemento ;
sCol	= 'Descricao';

If( cExisteRegistroRejeitado = 0 );
   If( sElm @= '' );
      cMensagemRegistro = 'A coluna    [ ' | sCol | ' ]    está em branco e é obrigatório ter algum valor';
      cExisteRegistroRejeitado = 1;
   EndIf;
EndIf;



# ===================================================================================
# Marca o registro para rejeitar 
# Verifica se o registro está duplicado no arquivo
# ===================================================================================
If( cExisteRegistroRejeitado = 0 );
  sDuplic = CellGetS( cCuboDestino, vCod_Elemento, 'Data Atualização' );
   If ( sDuplic @<> '' );
      cExisteRegistroRejeitado = 1;
      cMensagemRegistro = 'Registro duplicado no arquivo fonte.';
   EndIf;
EndIf;




# ===================================================================================
# DEBUG ou REJEIÇÃO
# Registra em arquivo os registros
# ===================================================================================
If( cExisteRegistroRejeitado = 1  %  cUtilizarDebug @= 'S' );
 
   #--------------------------------------------------------------------------------------------
   # Escreve o cabeçalho do arquivo de registros rejeitados
   #--------------------------------------------------------------------------------------------
   If( cExisteCabecalhoRejeitado  = 0 );
      
      sT01 = 'Code';      
      sT02 = 'Description';       
       
      sT03 = 'H1 Level 1';  
      sT04 = 'Description H1 Level 1';       
      sT05 = 'H1 Level 2';
      sT06 = 'Description H1 Level 2';   
      sT07 = 'H1 Level 3';
      sT08 = 'Description H1 Level 3'; 
       
      sT09 = 'H2 Level 1';  
      sT10 = 'Description H2 Level 1';       
      sT11 = 'H2 Level 2';
      sT12 = 'Description H2 Level 2';   
      sT13 = 'H2 Level 3';
      sT14 = 'Description H2 Level 3';   
       
      sT15 = 'BPC Code';          


      
      sTMsg = 'Descrição do erro';
      CellPutS(sTMsg,cCuboReject,cUsuario,cProcesso,'Título','Mensagem') ;
      CellPutS(sT01,cCuboReject,cUsuario,cProcesso,'Título','Coluna 01') ;
      CellPutS(sT02,cCuboReject,cUsuario,cProcesso,'Título','Coluna 02') ;
      CellPutS(sT03,cCuboReject, cUsuario,cProcesso, 'Título','Coluna 03') ;
      CellPutS(sT04,cCuboReject, cUsuario,cProcesso, 'Título','Coluna 04') ; 
      CellPutS(sT05,cCuboReject,cUsuario,cProcesso,'Título','Coluna 05') ;
      CellPutS(sT06,cCuboReject,cUsuario,cProcesso,'Título','Coluna 06') ;
      CellPutS(sT07,cCuboReject,cUsuario,cProcesso,'Título','Coluna 07') ; 
      CellPutS(sT08,cCuboReject,cUsuario,cProcesso,'Título','Coluna 08') ;
      CellPutS(sT09,cCuboReject, cUsuario,cProcesso, 'Título','Coluna 09') ;
      CellPutS(sT10,cCuboReject, cUsuario,cProcesso, 'Título','Coluna 10') ; 
      CellPutS(sT11,cCuboReject,cUsuario,cProcesso,'Título','Coluna 11') ;
      CellPutS(sT12,cCuboReject,cUsuario,cProcesso,'Título','Coluna 12') ;
      CellPutS(sT13,cCuboReject,cUsuario,cProcesso,'Título','Coluna 13') ; 
      CellPutS(sT14,cCuboReject,cUsuario,cProcesso,'Título','Coluna 14') ;
      CellPutS(sT15,cCuboReject,cUsuario,cProcesso,'Título','Coluna 15') ; 
       
    cExisteCabecalhoRejeitado = 1;
    
  EndIf;


    #----------------------------------------------------------------------------------------------
   # Escreve o registro no arquivo de rejeitados e pula o registro
   #---------------------------------------------------------------------------------------------- 
sReg01 = vCod_Elemento ; 
sReg02 = vDesc_Elemento;
    
sReg03 =  vH1_Nivel_1;
sReg04 =  vH1_Desc_Nivel_1;
sReg05 =  vH1_Nivel_2;
sReg06 =  vH1_Desc_Nivel_2;
sReg07 =  vH1_Nivel_3;
sReg08 =  vH1_Desc_Nivel_3;
sReg09 =  vH2_Nivel_1;
#sReg10 =  vH2_Desc_Nivel_1;
sReg11 =  vH2_Nivel_2;
#sReg12 =  vH2_Desc_Nivel_2;
#sReg13 =  vH2_Nivel_3;
#sReg14 =  vH2_Desc_Nivel_3;    
    

sReg15 =  vCod_BPC;   

#    #----------------------------------------------------------------------------------------------
#    # Escreve o registro no arquivo de rejeitados e pula o registro
#    #----------------------------------------------------------------------------------------------
   cQtdRegistroRejeitadoCubo = cQtdRegistroRejeitadoCubo + 1;
   vLinha = FILL( '0', 7 - LONG( Trim( NumberToString(cQtdRegistroRejeitadoCubo ) ) ) ) | Trim( NumberToString(cQtdRegistroRejeitadoCubo ) );
   
   # =================================================================================
   # Atualiza dimensão do Item Rejeitado
   # =================================================================================
   sElm = vLinha;
   sDim = 'SYS.D.Linhas' ;
   If( sElm @<> '' & DimIx( sDim, sElm ) = 0 );
      DimensionElementInsertDirect( sDim, '', sElm, 'N' );
   EndIf;
   
   CellPutS(sReg01, cCuboReject,cUsuario,cProcesso,vLinha,'Coluna 01') ;
   CellPutS(sReg02, cCuboReject, cUsuario,cProcesso,vLinha,'Coluna 02') ;
   CellPutS(sReg03, cCuboReject, cUsuario,cProcesso,vLinha,'Coluna 03') ;
   CellPutS(sReg04, cCuboReject, cUsuario,cProcesso,vLinha,'Coluna 04') ;  
   CellPutS(sReg05, cCuboReject, cUsuario,cProcesso,vLinha,'Coluna 05') ;
   CellPutS(sReg06, cCuboReject, cUsuario,cProcesso,vLinha,'Coluna 06') ;
   CellPutS(sReg07, cCuboReject, cUsuario,cProcesso,vLinha,'Coluna 07') ;
   CellPutS(sReg08, cCuboReject,cUsuario,cProcesso,vLinha,'Coluna 08') ;
   CellPutS(sReg09, cCuboReject, cUsuario,cProcesso,vLinha,'Coluna 09') ;
#   CellPutS(sReg10, cCuboReject, cUsuario,cProcesso,vLinha,'Coluna 10') ;
   CellPutS(sReg11, cCuboReject, cUsuario,cProcesso,vLinha,'Coluna 11') ;  
#   CellPutS(sReg12, cCuboReject, cUsuario,cProcesso,vLinha,'Coluna 12') ;
#   CellPutS(sReg13, cCuboReject, cUsuario,cProcesso,vLinha,'Coluna 13') ;
#   CellPutS(sReg14, cCuboReject, cUsuario,cProcesso,vLinha,'Coluna 14') ;      
   CellPutS(sReg15, cCuboReject, cUsuario,cProcesso,vLinha,'Coluna 15') ;      

   CellPutS( cMensagemRegistro, cCuboReject, cUsuario,cProcesso, vLinha,'Mensagem') ;
   
   # ============================
   # Limpa as variaveis e pula o registro
   # ============================
   IF( cExisteRegistroRejeitado = 1 );
      cExisteRegistroRejeitado = 0;
      cMensagemRegistro = '';
      cMensagemProcesso = ' Processo concluido com registros rejeitados. Verifique a aba Log Rejeitados' ;
      cTipoErro = 2;
      ItemSkip;
   EndIf;
  
EndIf; 



# ===================================================================================
# Grava os dados no cubo
# ===================================================================================
s01 = vDesc_Elemento;

s02 = vH1_Nivel_1;
s03 = vH1_Desc_Nivel_1;
s04 = vH1_Nivel_2;
s05 = vH1_Desc_Nivel_2;
s06 = vH1_Nivel_3;
s07 = vH1_Desc_Nivel_3;

s08 = vH2_Nivel_1;
s09 = vH2_Nivel_2;

s10 = vCod_BPC;

CellPutS ( s01, cCuboDestino, vCod_Elemento, 'Description' );
CellPutS ( s02, cCuboDestino, vCod_Elemento, 'H1 Level 1' );
CellPutS ( s03, cCuboDestino, vCod_Elemento, 'Description H1 Level 1' );
CellPutS ( s04, cCuboDestino, vCod_Elemento, 'H1 Level 2' );
CellPutS ( s05, cCuboDestino, vCod_Elemento, 'Description H1 Level 2' );
CellPutS ( s06, cCuboDestino, vCod_Elemento, 'H1 Level 3' );
CellPutS ( s07, cCuboDestino, vCod_Elemento, 'Description H1 Level 3' );

CellPutS ( s08, cCuboDestino, vCod_Elemento, 'H2 Level 1' );
CellPutS ( s09, cCuboDestino, vCod_Elemento, 'H2 Level 2' );

CellPutS ( s10, cCuboDestino, vCod_Elemento, 'BPC Code' );

CellPutS ( TimSt( cHorarioInicio , '\Y-\m-\d \hh\im\ss' ), cCuboDestino, vCod_Elemento, 'Data Atualização' );
#endregion
#region Epilog

#****Begin: Generated Statements***
#****End: Generated Statements****


# ===============================================================================================================
#EPILOGCOMMENT        Descrição:  Habilita log do cubo de destino, realiza CubeSaveData e registra log de status do processo.
# ===============================================================================================================



# =================================================================================
# Registra o Log de Erro ou Sucesso no Cubo de Controle
# =================================================================================
If( cTipoErro = 0 );
   cMensagemProcesso  = ' Processo concluído com sucesso';
EndIf;


s01	= TimSt( cHorarioInicio , '\Y-\m-\d \hh\im\ss' );
s02 	= cUsuario ;
s03 	= cMensagemProcesso ;
s04 	= sContadorRegistro  ;
s05 	= TimSt( Now - cHorarioInicio, ' \h:\i:\s') ;

CellPutS( s01 , cCuboParametro, cProcesso, 'DataHora Fim' );
CellPutS( s02 , cCuboParametro, cProcesso, 'Usuário' );
CellPutS( s03 , cCuboParametro, cProcesso, 'Mensagem de Log' );
CellPutS( s04 , cCuboParametro, cProcesso, 'Registros Processados' );
CellPutS( s05 , cCuboParametro, cProcesso, 'Tempo de Execução' );


If( cTipoErro = 2 );
  ItemReject( cMensagemProcesso );
EndIf;
#endregion